buildscript {
	repositories {
		mavenCentral()
		maven { url 'https://maven.fabricmc.net/' }
		maven { url 'https://jitpack.io/' }
		jcenter()
	}
	dependencies {
		classpath 'com.github.Chocohead:Fabric-Loom:d823377'
		classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
	}
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'fabric-loom'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = file('../version.txt').text.trim()+file('version-suffix.txt').text.trim()
group = project.maven_group

repositories {
	maven {
		name = 'Jitpack'
		url 'https://jitpack.io/'
	}
}

configurations {
	shade
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	mappings loom.fromCommit('minecraft-cursed-legacy/Plasma', 'e84b183') {spec ->
		spec.version = 'b1.7.3-17'
	}

	modImplementation("com.github.minecraft-cursed-legacy:cursed-fabric-loader:${project.loader_version}") {
		transitive false
	}
	modImplementation files('./modmenu.jar')
	
	implementation files('../common/build/libs/ears-common-mixin-vlegacy.jar')
	shade files('../common/build/libs/ears-common-mixin-vlegacy.jar')
}

minecraft {
	//Skip old versions of libraries which Mojang use but we don't need
	addLibraryFilter {library ->
		return !(library.startsWith('net.minecraft:launchwrapper:') ||
					library.startsWith('net.sf.jopt-simple:jopt-simple:') ||
					library.startsWith('org.ow2.asm:asm-all:'))
	}
}

shadowJar {
	configurations = [project.configurations.shade]

	exclude 'module-info.class'
	exclude 'META-INF/maven/**'
}

processResources {
	inputs.property 'version', project.version

	from(sourceSets.main.resources.srcDirs) {
		include 'fabric.mod.json'
		expand 'version': project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'fabric.mod.json'
	}
}

remapJar {
	dependsOn shadowJar
	input = file(shadowJar.archivePath)
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

build.doLast {
	file(shadowJar.archivePath).delete()
}

compileJava.doFirst {
	'../common/replace-version.sh src/main/java/com/unascribed/ears/EarsPlatformVersion.java'.execute()
}