buildscript {
	repositories {
		mavenCentral()
		gradlePluginPortal()
		maven { url 'https://maven.fabricmc.net' }
		maven { url 'https://repo.sleeping.town' }
		maven { url 'https://maven.minecraftforge.net/' }
	}
	dependencies {
		classpath 'agency.highlysuspect:voldeloom:2.0-SNAPSHOT'
		classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
		classpath 'me.tatarka:gradle-retrolambda:3.7.1'
	}
	configurations.all {
		resolutionStrategy.cacheChangingModulesFor 120, 'seconds'
	}
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'agency.highlysuspect.voldeloom'

sourceCompatibility = 7
targetCompatibility = 7

archivesBaseName = project.archives_base_name
version = file('../version.txt').text.trim()+file('version-suffix.txt').text.trim()
group = project.maven_group

repositories {
	maven { url 'https://jitpack.io/' }
	maven { url 'https://maven.fabricmc.net/' }
	maven { url 'https://repo.sleeping.town' }
//	maven { url 'https://files.minecraftforge.net/maven' }
	maven { url 'https://repo.spongepowered.org/maven' }
}

configurations {
	shade
}

minecraft {
	srgsAsFallback = true
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	forge "net.minecraftforge:forge:1.4.7-6.6.2.534:universal@zip"
	mappings files("mcp726a.zip") // TODO don't do this
	
	implementation files('../common/build/libs/ears-common-agent-vlegacy.jar')
	shade files('../common/build/libs/ears-common-agent-vlegacy.jar')
}

shadowJar {
	configurations = [project.configurations.shade]
	relocate 'com.github.steveice10', 'com.unascribed.ears.repackage.com.github.steveice10'
	relocate 'com.google', 'com.unascribed.ears.repackage.com.google'
	
	exclude 'module-info.class'
	exclude 'META-INF/maven/**'
}

processResources {
	inputs.property "version", project.version
	
	duplicatesStrategy 'include'

	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

remapJar {
	dependsOn shadowJar
	input = file(shadowJar.archivePath)
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

jar {
	from "LICENSE"
	manifest {
		attributes (
			'FMLCorePlugin': 'com.unascribed.ears.common.agent.OldForgeCoremod'
		)
	}
}

build.doLast {
	file(shadowJar.archivePath).delete()
}

compileJava.doFirst {
	'../common/replace-version.sh src/main/java/com/unascribed/ears/Ears.java src/main/java/com/unascribed/ears/EarsPlatformVersion.java'.execute()
}
